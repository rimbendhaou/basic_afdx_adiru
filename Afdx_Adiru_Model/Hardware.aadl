package Hardware
public
  WITH AFDX;
  WITH AFDX_Properties;
  WITH dataType;
  with ARINC653;
  with ID_Properties;

------------------------------------------------------------------
--- 1. Hardware_Layer : The Platform Execution
------------------------------------------------------------------
  SYSTEM Hardware_Layer
  END Hardware_Layer;
  
  SYSTEM IMPLEMENTATION Hardware_Layer.i
    SUBCOMPONENTS
      CPM_1 : SYSTEM Module.imp1; -- Processor or System
      CPM_2 : SYSTEM Module.i; -- Processor or System
      CPM_3 : SYSTEM Module.i; -- Processor or System
     
      WIRE_1 : BUS AFDX::Wire; -- Bus
      WIRE_2 : BUS AFDX::Wire; -- Bus
      WIRE_3 : BUS AFDX::Wire; -- Bus
      WIRE_4 : BUS AFDX::Wire; -- Bus
      WIRE_5 : BUS AFDX::Wire; -- Bus
    
      Switch_1 : DEVICE Switch; -- Device or System
      Switch_2 : DEVICE Switch; -- Device or System
     
      RDC_Sensor: device RDC; -- RDC
    CONNECTIONS
      WIRE_1_CPM_1: BUS ACCESS WIRE_1->CPM_1.afdx;
      WIRE_1_SW_1:  BUS ACCESS WIRE_1->Switch_1.afdx_p1;
      WIRE_3_SW_1:  BUS ACCESS WIRE_3->Switch_1.afdx_p3;
      WIRE_1_CPM_2: BUS ACCESS WIRE_3->CPM_2.afdx;
      WIRE_2_Switch_1:  BUS ACCESS WIRE_2->Switch_1.afdx_p4;
      WIRE_2_Switch_2:  BUS ACCESS WIRE_2->Switch_2.afdx_p1;
      WIRE_4_Switch_2:  BUS ACCESS WIRE_4->Switch_2.afdx_p4;
      WIRE_4_CPM_3: BUS ACCESS WIRE_4->CPM_3.afdx;
      WIRE_5_RDC: BUS ACCESS WIRE_5->RDC_Sensor.afdx;
      WIRE_5_Switch_1:  BUS ACCESS WIRE_5->Switch_1.afdx_p2;
  END Hardware_Layer.i;  
 
 
------------------------------------------------------------------
--- 1.1 Module
------------------------------------------------------------------
  SYSTEM Module
    FEATURES
      afdx : REQUIRES BUS ACCESS AFDX::Wire;
  END Module;

  SYSTEM IMPLEMENTATION Module.i
    SUBCOMPONENTS
      --CPU : PROCESSOR CPU_with_PCI;
      --modified for resolute validation
      CPU : PROCESSOR CPU_with_PCI.i;
      AFDX_EndSystem : DEVICE AFDX_Card;
      PCI_BUS : BUS;
    CONNECTIONS
      CPU_PCI: BUS ACCESS PCI_BUS->CPU.pci;
      AFDX_ES_PCI: BUS ACCESS PCI_BUS->AFDX_EndSystem.pci;
      AFDX_EXT: BUS ACCESS AFDX_EndSystem.afdx->afdx;
  END Module.i;
  
  ------------------------------------------------------------------
---  Module implementation (added for ARINC653 validation Major Frame )
------------------------------------------------------------------
  SYSTEM IMPLEMENTATION Module.imp1
    SUBCOMPONENTS
      --CPU : PROCESSOR CPU_with_PCI;
      --modified for ARINC653 validation Major Frame
      CPU : PROCESSOR CPU_with_PCI.imp1;
      AFDX_EndSystem : DEVICE AFDX_Card;
      PCI_BUS : BUS;
    CONNECTIONS
      CPU_PCI: BUS ACCESS PCI_BUS->CPU.pci;
      AFDX_ES_PCI: BUS ACCESS PCI_BUS->AFDX_EndSystem.pci;
      AFDX_EXT: BUS ACCESS AFDX_EndSystem.afdx->afdx;
  END Module.imp1;
  

------------------------------------------------------------------
--- 1.2 CPU_with_PCI
------------------------------------------------------------------
  PROCESSOR CPU_with_PCI
    FEATURES
      pci : REQUIRES BUS ACCESS;
    properties
      Scheduling_Protocol => (POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL);
       
  END CPU_with_PCI;
  
 ------------------------------------------------------------------
--- 1.2.1 CPU_with_PCI Processor implementation (added for ARINC653 validation Major Frame)
------------------------------------------------------------------
  processor implementation CPU_with_PCI.imp1
  subcomponents
    Partition_P1 : virtual processor;
    Partition_P2 : virtual processor;
   
  properties
    ARINC653::Module_Major_Frame => 50ms;

    ARINC653::Module_Schedule => (
    [Partition => reference (Partition_P1);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;],
    [Partition => reference (Partition_P2);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;]
    
    );
     ARINC653::HM_Error_ID_Levels =>
           ([ErrorIdentifier => 1; Description => "Deadline Missed"; ErrorLevel => Process_Level ; ErrorCode => Deadline_Miss;],
     	    [ErrorIdentifier => 2; Description => "Application Error"           ; ErrorLevel => Process_Level ; ErrorCode => Application_Error;],
     	    [ErrorIdentifier => 3; Description => "Numeric Error"               ; ErrorLevel => Process_Level ; ErrorCode => Numeric_Error;],
     	    [ErrorIdentifier => 4; Description => "Illegal Request"             ; ErrorLevel => Process_Level ; ErrorCode => Illegal_Request;],
     	    [ErrorIdentifier => 5; Description => "Stack Overflow"              ; ErrorLevel => Process_Level ; ErrorCode => Stack_Overflow;],
     	    [ErrorIdentifier => 6; Description => "Memory Violation"            ; ErrorLevel => Process_Level ; ErrorCode => Memory_Violation;],
     	    [ErrorIdentifier => 7; Description => "Hardware Fault"              ; ErrorLevel => Process_Level ; ErrorCode => Hardware_Fault;],
     	    [ErrorIdentifier => 8; Description => "Power Failure"               ; ErrorLevel => Process_Level ; ErrorCode => Power_Fail;]);

    ARINC653::HM_Error_ID_Actions =>
           ([ErrorIdentifier => 1 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 2 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 3 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 4 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 5 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 6 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 7 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 8 ; Description => "Ignore the error"; Action => "ignore";]);
  end CPU_with_PCI.imp1;
   
------------------------------------------------------------------
--- 1.2.1 CPU_with_PCI Processor implementation (added for resolute validation)
------------------------------------------------------------------
  processor implementation CPU_with_PCI.i
  subcomponents
    Partition_P1 : virtual processor;
    Partition_P2 : virtual processor;
    Partition_P3 : virtual processor;
    Partition_P4 : virtual processor;
  properties
    ARINC653::Module_Major_Frame => 100ms;

    ARINC653::Module_Schedule => (
    [Partition => reference (Partition_P1);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;],
    [Partition => reference (Partition_P2);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;],
    [Partition => reference (Partition_P3);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;],
    [Partition => reference (Partition_P4);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;]
    );
     ARINC653::HM_Error_ID_Levels =>
           ([ErrorIdentifier => 1; Description => "Deadline Missed"; ErrorLevel => Process_Level ; ErrorCode => Deadline_Miss;],
     	    [ErrorIdentifier => 2; Description => "Application Error"           ; ErrorLevel => Process_Level ; ErrorCode => Application_Error;],
     	    [ErrorIdentifier => 3; Description => "Numeric Error"               ; ErrorLevel => Process_Level ; ErrorCode => Numeric_Error;],
     	    [ErrorIdentifier => 4; Description => "Illegal Request"             ; ErrorLevel => Process_Level ; ErrorCode => Illegal_Request;],
     	    [ErrorIdentifier => 5; Description => "Stack Overflow"              ; ErrorLevel => Process_Level ; ErrorCode => Stack_Overflow;],
     	    [ErrorIdentifier => 6; Description => "Memory Violation"            ; ErrorLevel => Process_Level ; ErrorCode => Memory_Violation;],
     	    [ErrorIdentifier => 7; Description => "Hardware Fault"              ; ErrorLevel => Process_Level ; ErrorCode => Hardware_Fault;],
     	    [ErrorIdentifier => 8; Description => "Power Failure"               ; ErrorLevel => Process_Level ; ErrorCode => Power_Fail;]);

    ARINC653::HM_Error_ID_Actions =>
           ([ErrorIdentifier => 1 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 2 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 3 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 4 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 5 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 6 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 7 ; Description => "Ignore the error"; Action => "ignore";],
            [ErrorIdentifier => 8 ; Description => "Ignore the error"; Action => "ignore";]);
  end CPU_with_PCI.i;
  
  
------------------------------------------------------------------
-- 1.3 AFDX_Card
------------------------------------------------------------------
  DEVICE AFDX_Card EXTENDS AFDX::End_System
    FEATURES
      pci  : REQUIRES BUS ACCESS;
    PROPERTIES
      AFDX_Properties::Supported_Port_Speeds => (100 MBytesps);
      ID_Properties::Device_Type => EndSystem;
  END AFDX_Card;
  
------------------------------------------------------------------
--- 1.4 AFDXSwitch - An implementation with 4 port 
------------------------------------------------------------------
  DEVICE Switch EXTENDS AFDX::Switch
	FEATURES
		Afdx_p1 : REQUIRES BUS ACCESS AFDX::Wire;
		Afdx_p2 : REQUIRES BUS ACCESS AFDX::Wire;
		Afdx_p3 : REQUIRES BUS ACCESS AFDX::Wire;
		Afdx_p4 : REQUIRES BUS ACCESS AFDX::Wire;
    PROPERTIES
      AFDX_Properties::Supported_Port_Speeds => (10 MBytesps,100 MBytesps);
      ID_Properties::Device_Type => Switch;
  END Switch;
  
  device RDC
		features
			Pressure: out event data port dataType::pressure;
			Afdx: requires bus access AFDX::Wire;
  	Properties
  		ID_Properties::Device_Type => Sensor;
	end RDC;   
end Hardware;